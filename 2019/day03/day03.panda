module day03

require java:collections

import java.io.File
import java.nio.file.Files
import java.lang.Math

main {
    List wires = Files.readAllLines(new File(System.getProperty("input")).toPath())
    String[] first = (wires.get(0) as String).split(",")
    String[] second = (wires.get(1) as String).split(",")

    /*
     * Map first wire into <X, List<Y>> the first map using the moving point
     */

    Map firstMap = new HashMap()
    mut MovingPoint lastPoint = new MovingPoint(0, 0, 0)

    foreach (String move : first) {
        lastPoint = lastPoint.movePoint(firstMap, move)
    }

    /*
     * Map second wire into <X, List<Y>> the second map using the moving point
     */

    Map secondMap = new HashMap()
    lastPoint = new MovingPoint(0, 0, 0)

    foreach (String move : second) {
       lastPoint = lastPoint.movePoint(secondMap, move)
    }

    /*
     * Search for the crossing points
     */

    List crossPoints = new ArrayList()

    foreach (Entry firstEntry : firstMap.entrySet()) {
        nil List secondY = secondMap.get(firstEntry.getKey()) as List

        if secondY == null {
            continue
        }

        foreach (MovingPoint firstPoint : firstEntry.getValue() as List) {
            foreach (MovingPoint secondPoint : secondY) {
                if firstPoint.x == secondPoint.x && firstPoint.y == secondPoint.y {
                    crossPoints.add(new MovingPoint(firstPoint.x, firstPoint.y, firstPoint.fullSteps + secondPoint.fullSteps))
                }
            }
        }
    }

    /*
     * Select the nearest point
     */

    mut Int manhattanDistance = Int.MAX_VALUE

    foreach (MovingPoint point : crossPoints) {
        Int currentDistance = Math.abs(point.x) + Math.abs(point.y)

        if currentDistance < manhattanDistance {
            manhattanDistance = currentDistance
        }
    }

    log 'Manhattan distance: ' + manhattanDistance

    /*
     * Select the fewest combination
     */

     mut Int fewestCombination = Int.MAX_VALUE

     foreach (MovingPoint points : crossPoints) {
        if (points.fullSteps < fewestCombination) {
            fewestCombination = points.fullSteps
        }
     }

     log 'Fewest combination: ' + fewestCombination
}

/*
 * Cancerous class
 */
local class MovingPoint {

    local Int x
    local Int y
    local Int fullSteps

    constructor(Int xValue, Int yValue, Int stepsValue) {
        this.x = xValue
        this.y = yValue
        this.fullSteps = stepsValue
    }

    local MovingPoint movePoint(Map map, String move) {
        String direction = move.substring(0, 1)
        Int steps = Int.parseInt(move.substring(1))

        if direction == 'U' {
            for (mut Int i = 1; i <= steps; i++ ) {
                put(map, x, y + i, fullSteps + i)
            }
            return new MovingPoint(x, y + steps, fullSteps + steps)
        }
        else if direction == 'D' {
            for (mut Int i = 1; i <= steps; i++ ) {
                put(map, x, y - i, fullSteps + i)
            }
            return new MovingPoint(x, y - steps, fullSteps + steps)
        }
        else if direction == 'L' {
            for (mut Int i = 1; i <= steps; i++ ) {
                put(map, x - i, y, fullSteps + i)
            }
            return new MovingPoint(x - steps, y, fullSteps + steps)
        }
        else if direction == 'R' {
            for (mut Int i = 1; i <= steps; i++ ) {
                put(map, x + i, y, fullSteps + i)
            }
            return new MovingPoint(x + steps, y, fullSteps + steps)
        }
    }

    local static put(Map map, Int x, Int y, Int fullSteps) {
        mut nil List list = map.get(x) as List

        if list == null {
            list = new ArrayList()
            map.put(x, list)
        }

        list.add(new MovingPoint(x, y, fullSteps))
    }

}